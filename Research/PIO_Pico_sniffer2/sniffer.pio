; Program name
.program sniffer

    pull block                  ; Pull from FIFO to OSR (only once)
    mov y, osr                  ; Copy value from OSR to y scratch register

.wrap_target
    mov x, y                    ; Initialize counter variable

    wait 0 gpio 8 [31]
    wait 1 gpio 8               ; wait image ready high

pixelout:
        wait 1 gpio 9 [11]      ; wait clock low and then delay 96ns

        set pins, 0 [31]        ; set adc ready low

        nop [31]
        nop [10]                ; wait 600 ns (75 8ns periods total)

        in pins, 8              ; read ADC value
        in null, 24             ; shift right by 24
        push block              ; push color value

        set pins, 1             ; set adc ready high

        wait 0 gpio 9 [31]      ; wait clock high
    jmp x-- pixelout            ; loop through all the pixels

    irq 0                       ; raise IRQ0
.wrap

% c-sdk {

#define DATA_PINS 0
#define N_DATA_PINS 8
#define IMG_READY_PIN 8
#define CLK_PIN 9
#define ADC_READY_PIN 10

static inline void sniffer_program_init(PIO pio, uint sm, uint offset) {
    pio_sm_config c = sniffer_program_get_default_config(offset);

    sm_config_set_in_pins(&c, DATA_PINS);
    pio_sm_set_consecutive_pindirs(pio, sm, DATA_PINS, N_DATA_PINS, false);
    sm_config_set_in_shift(&c, true, false, N_DATA_PINS);

    pio_sm_set_consecutive_pindirs(pio, sm, IMG_READY_PIN, 1, false);
    pio_sm_set_consecutive_pindirs(pio, sm, CLK_PIN, 1, false);

    sm_config_set_set_pins(&c, ADC_READY_PIN, 1);
    pio_sm_set_consecutive_pindirs(pio, sm, ADC_READY_PIN, 1, true);

//    sm_config_set_clkdiv(&c, 5);

    for (uint8_t i = DATA_PINS; i != (DATA_PINS + N_DATA_PINS); i++) pio_gpio_init(pio, i);
    pio_gpio_init(pio, IMG_READY_PIN);
    pio_gpio_init(pio, CLK_PIN);
    pio_gpio_init(pio, ADC_READY_PIN);

    pio_sm_init(pio, sm, offset, &c);

    // pio_sm_set_enabled(pio, sm, true);
}
%}